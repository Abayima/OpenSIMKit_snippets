!exists($$[QT_INSTALL_HEADERS]/QtAddOnSerialPort) {
    INCLUDEPATH += $$SERIALPORT_BUILD_ROOT/include $$SERIALPORT_BUILD_ROOT/include/QtAddOnSerialPort

    SERIALPORT_BUILD_SUBDIR = src/serialport
    win32 {
        CONFIG(debug, debug|release) {
            SERIALPORT_BUILD_SUBDIR = $$SERIALPORT_BUILD_SUBDIR/debug
        } else {
            SERIALPORT_BUILD_SUBDIR = $$SERIALPORT_BUILD_SUBDIR/release
        }
    }

    LIBS += -L$$SERIALPORT_BUILD_ROOT/$$SERIALPORT_BUILD_SUBDIR
}

qtAddLibrary(SerialPort)

mac {
    LIBS -= -framework SerialPort$${QT_LIBINFIX}

   if(!debug_and_release|build_pass):CONFIG(debug, debug|release) {
       LIBS += -lSerialPort$${QT_LIBINFIX}_debug
   } else {
       LIBS += -lSerialPort$${QT_LIBINFIX}
   }
}

INCLUDEPATH -= $$[QT_INSTALL_HEADERS]/SerialPort
INCLUDEPATH += $$[QT_INSTALL_HEADERS]/QtAddOnSerialPort
